apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = rootProject.ext.version
group = 'com.jraska'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionName version
    versionCode rootProject.ext.versionCode
  }

  compileOptions {
    sourceCompatibility rootProject.ext.sourceCompatibility
    targetCompatibility rootProject.ext.sourceCompatibility
  }

  lintOptions {
    abortOnError true
    disable 'UnusedAttribute'
  }
}

dependencies {
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier = 'sources'
}

artifacts {
  archives sourcesJar
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId 'com.jraska'
        artifactId 'falcon'
        version rootProject.ext.version

        from components.release
        artifact sourcesJar

        pom {
          name = 'com.jraska'
          description = 'Better Android screenshots'
          url = 'https://github.com/jraska/Falcon'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'jraska'
              name = 'Josef Raska'
              email = 'josef.raska@gmail.com'
            }
          }

          // Version control info - if you're using GitHub, follow the
          // format as seen here
          scm {
            connection = 'scm:git:github.com/jraska/Falcon.git'
            developerConnection = 'scm:git:ssh://github.com/jraska/Falcon.git'
            url = 'https://github.com/getstream/stream-chat-android/tree/main'
          }
        }
      }
    }
  }
}

// Adding these directly into signing {} below didn't work :idk
ext["signing.keyId"] = System.getenv("SIGNING_KEY_ID")
ext["signing.password"] = System.getenv("SIGNING_KEY_PASSWORD")
ext["signing.secretKeyRingFile"] = "$buildDir/signing-key.gpg"

signing {
  sign publishing.publications
}
